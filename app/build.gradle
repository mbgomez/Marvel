apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

def getSignPassword = { ->
    def name = project.hasProperty("password_jks") ? project.password_jks.toString() : properties.get("password_jks")
    return name
}

def getVerCode = { ->
    def code = project.hasProperty("versionCode") ? project.versionCode.toInteger() : 10000
    return code
}

def getVerName = { ->
    def name = project.hasProperty("versionName") ? project.versionName.toString() : "1.0"
    return name
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.realm:realm-gradle-plugin:3.1.4"
    }
}
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.marvel.comic_shop"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode getVerCode()
        versionName getVerName() + "." + versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        /*jackOptions {
            enabled true
        }
        */
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file("../comicShop.jks")
            storePassword getSignPassword()
            keyAlias "comicShop"
            keyPassword getSignPassword()
        }
        debug {
            storeFile new File("${System.properties['user.home']}/.android/debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
        }
        debug {
            /*applicationIdSuffix ".debug"*/
            manifestPlaceholders = [applicationLabel: "@string/app_name" + "Dev"]
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    android.applicationVariants.all { variant ->
        //change apk filename
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, outputFile.name.replace("app", "comic-shop").replace(".apk", "-" + versionName + ".apk"))
            }
        }

        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "aligned " + output.outputFile
                println "unaligned " + output.packageApplication.outputFile

                File unaligned = output.packageApplication.outputFile
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
    }

}

checkstyle {
    toolVersion = '7.6.1'
}

tasks.withType(Checkstyle) {
    exclude '**/LogJam.java'
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'



    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    testCompile 'junit:junit:4.12'
}

preBuild.dependsOn('checkstyle')
assemble.dependsOn('lint')
check.dependsOn('checkstyle')